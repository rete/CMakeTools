function( cmaketools_generate_standard_config )
    cmake_parse_arguments( ARG "NO_EXPORT" "PROJECT;TEMPLATE;DESTINATION;COMPATIBILITY;VERSION" "" ${ARGN} )
    set( CONFIG_NAME ${PROJECT_NAME} )
    set( TEMPLATE_FILE ${CMAKETOOLS_TEMPLATES_DIR}/TemplateConfig.cmake.in )
    set( DESTINATION ${CMAKETOOLS_PKGCFG_DIR} )
    set( COMPATIBILITY ExactVersion )
    set( CONFIG_VERSION ${PROJECT_VERSION} )
    if( ARG_PROJECT )
        set( CONFIG_NAME ${ARG_PROJECT} )
    endif()
    if( ARG_TEMPLATE )
        set( TEMPLATE_FILE ${ARG_TEMPLATE} )
    endif()
    if( ARG_DESTINATION )
        set( DESTINATION ${ARG_DESTINATION} )
    endif()
    if( ARG_COMPATIBILITY )
        set( COMPATIBILITY ${ARG_COMPATIBILITY} )
    endif()
    if( ARG_VERSION )
        set( CONFIG_VERSION ${ARG_VERSION} )
    endif()
    # See: https://cmake.org/cmake/help/v3.15/module/CMakePackageConfigHelpers.html
    include( CMakePackageConfigHelpers )
    configure_package_config_file(
        ${TEMPLATE_FILE} ${PROJECT_BINARY_DIR}/${CONFIG_NAME}Config.cmake
        INSTALL_DESTINATION ${DESTINATION}
        )
    write_basic_package_version_file(
        ${CONFIG_NAME}ConfigVersion.cmake
        COMPATIBILITY ${COMPATIBILITY}
        )
    get_property( PROJECT_CONFIG_VARS GLOBAL PROPERTY PROJECT_CONFIG_VARS )
    if( PROJECT_CONFIG_VARS )
        get_property( PROJECT_CONFIG_PREFIX_VARS GLOBAL PROPERTY PROJECT_CONFIG_PREFIX_VARS )
        set( CONFIG_VARS_FILE ${CMAKE_CURRENT_BINARY_DIR}/${CONFIG_NAME}Variables.cmake )
        file( WRITE ${CONFIG_VARS_FILE} "# CMake file generated by CMakeTools ${PROJECT_VERSION}\n" )
        file( APPEND ${CONFIG_VARS_FILE} "# Do not edit it !\n\n" )
        foreach( var ${PROJECT_CONFIG_VARS} )
            get_property( var_value GLOBAL PROPERTY CONFIG_EXPORT_${var} )
            if( "${var}" IN_LIST PROJECT_CONFIG_PREFIX_VARS )
                file( APPEND ${CONFIG_VARS_FILE} "set( ${var} \"\$\{PACKAGE_PREFIX_DIR\}/${var_value}\" CACHE STRING \"\" )\n" )
            else()
                file( APPEND ${CONFIG_VARS_FILE} "set( ${var} \"${var_value}\" CACHE STRING \"\" )\n" )
            endif()
            # file( APPEND ${CONFIG_VARS_FILE} "set( ${var} \"${var_value}\" CACHE STRING \"\" )\n" )
        endforeach()
        file( APPEND ${CONFIG_VARS_FILE} "\n" )
    endif()
    #--- Install configuration files
    install(
        FILES
        ${CMAKE_CURRENT_BINARY_DIR}/${CONFIG_NAME}Config.cmake
        ${CMAKE_CURRENT_BINARY_DIR}/${CONFIG_NAME}ConfigVersion.cmake
        ${CONFIG_VARS_FILE}
        DESTINATION
        ${DESTINATION}
        )
    if( NOT ARG_NO_EXPORT )
        #--- Generate targets file
        install( EXPORT ${CONFIG_NAME}
            FILE ${CONFIG_NAME}Config-targets.cmake
            NAMESPACE ${CONFIG_NAME}::
            DESTINATION ${DESTINATION}
            )
    endif()
endfunction()